#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          to be called from test21.30
#          Tests if the MaxNodesPU limit is enforced
############################################################################
# Copyright (C) 2014 SchedMD LLC
# Written by Nathan Yee <nyee32@schedmd.com>
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################

proc inc21_30_16 { wait_reason } {
	global salloc srun acct number bin_sleep maxnodespu_num

	log_info "Starting MaxNodesPerUser test"

	set job_id 0

	# Raise an error to abort the catch block
	set exception_code [catch {

		spawn $salloc --account=$acct -N$maxnodespu_num -t1 --exclusive $srun $bin_sleep 2
		expect {
			-re "Granted job allocation ($number)" {
				set job_id $expect_out(1,string)
				log_debug "Job $job_id was submitted"
			}
			-re "error" {
				error "Job allocation should not have failed"
			}
			timeout {
				error "salloc is not responding"
			}
			eof {
				wait
			}
		}

		if { $job_id == 0 } {
			error "Job was not submitted"
		} else {
			# Cancel job
			cancel_job $job_id
		}

		spawn $salloc --account=$acct -N[expr $maxnodespu_num + 1] -t1 --exclusive $srun $bin_sleep 2
		expect {
			-re "Pending job allocation ($number)" {
				set job_id $expect_out(1,string)
				log_debug "This error is expected. Do not worry"
			}
			-re "Granted job allocation" {
				error "salloc should not have granted an allocation"
			}
			timeout {
				error "salloc is not responding"
			}
			eof {
				wait
			}
		}

		if { $job_id == 0 } {
			error "Job was not submitted"
		}

		subtest [check_reason $job_id $wait_reason] "Job should have wait reason $wait_reason"

	} message] ; # Store the error message in $message

	# Cancel job
	cancel_job $job_id

	# Convert any errors into failures (after cleaning up)
	if {$exception_code == 1} { ; # errors only
		fail "Failure testing MaxNodesPerUser: $message"
	}
}
