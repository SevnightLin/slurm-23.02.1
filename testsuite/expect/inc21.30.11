#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          to be called from test21.30
#          Tests if the GrpMem limit is enforced
############################################################################
# Copyright (C) 2013 SchedMD LLC
# Written by Nathan Yee <nyee32@schedmd.com>
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################

proc inc21_30_11 { wait_reason } {
	global salloc srun acct bin_sleep grpmem_num number
	global test_select_type_params

	if {![param_contains [get_config_param "SelectTypeParameters"] "CR_*MEMORY"]} {
		log_warn "Memory management not configured"
		return
	}

	log_info "Starting GrpMem test"
	set job_id1 0
	set job_id2 0

	# Raise an error to abort the catch block
	set exception_code [catch {

		spawn $salloc --account=$acct -N1 --mem=$grpmem_num -t1 $srun $bin_sleep 10
		expect {
			-re "Granted job allocation ($number)" {
				set job_id1 $expect_out(1,string)
				log_debug "Job $job_id1 has been submitted"
			}
			-re "Pending job allocation ($number)" {
				set job_id1 $expect_out(1,string)
				error "Job should have run but did not"
			}
			-re "error" {
				error "Job allocation should not have failed"
			}
			timeout {
				error "salloc not responding"
			}
			eof {
				wait
			}
		}

		spawn $salloc --account=$acct -N1 --mem=$grpmem_num -t1 $srun $bin_sleep 10
		expect  {
			-re "Pending job allocation ($number)" {
				set job_id2 $expect_out(1,string)
				log_debug "Job $job_id2 is waiting for resources. This is expected"
			}
			-re "Granted job allocation ($number)" {
				error "Job should have pended, but did not"
			}
			timeout {
				error "salloc not responding"
			}
			eof {
				wait
			}
		}

		subtest [check_reason $job_id2 $wait_reason] "Job should have wait reason $wait_reason"

	} message] ; # Store the error message in $message

	# Cancel both jobs
	cancel_job $job_id1
	cancel_job $job_id2

	# Convert any errors into failures (after cleaning up)
	if {$exception_code == 1} { ; # errors only
		fail "Failure testing Group Memory: $message"
	}
}
